#! /usr/bin/perl
# $OpenBSD: dolock,v 1.2 2011/04/13 08:37:16 espie Exp $
# Copyright (c) 2004 Marc Espie <espie@openbsd.org>
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

use strict;
use warnings;
use Fcntl;
use File::Basename;

if (@ARGV == 0) {
	exit(1);
}
my $fname = shift;
my $dname = dirname $fname;
my $done = 0;

unless (-e $dname) {
	require File::Path;
	File::Path::mkpath($dname);
}
if (!-d $dname) {
	print STDERR "lock directory location $dname is not a directory\n";
	exit(1);
}
if (!-w _) {
	print STDERR "you don't have permission to write into $dname\n";
	exit(1);
}

while(1) {
	if (sysopen my $handle, $fname, O_WRONLY | O_EXCL | O_CREAT, 0666) {
		if (@ARGV)  {
			print $handle join("\n", @ARGV), "\n";
		}
		close($handle);
		exit(0);
	}
	unless ($done) {
		print STDERR "Awaiting lock $fname\n";
		$done = 1;
	}
	sleep(1);
}
